def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
// START: FlutterFire Configuration
apply plugin: 'com.google.gms.google-services'
// END: FlutterFire Configuration
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
// imgly video sdk
apply plugin: 'ly.img.android.sdk'
apply plugin: 'kotlin-android'

// Comment out the modules you don't need, to save size.
IMGLY.configure {
    modules {
        include 'ui:text'
        include 'ui:focus'
        include 'ui:frame'
        include 'ui:brush'
        include 'ui:filter'
        include 'ui:sticker'
        include 'ui:overlay'
        include 'ui:transform'
        include 'ui:adjustment'
        include 'ui:text-design'
        include 'ui:video-trim'
        include 'ui:video-library'
        include 'ui:video-composition'
        include 'ui:audio-composition'
        include 'ui:giphy-sticker'

        // This module is big, remove the serializer if you don't need that feature.
        include 'backend:serializer'

        // Remove the asset packs you don't need, these are also big in size.
        include 'assets:font-basic'
        include 'assets:frame-basic'
        include 'assets:filter-basic'
        include 'assets:overlay-basic'
        include 'assets:sticker-shapes'
        include 'assets:sticker-emoticons'
        include 'assets:sticker-animated'

        include 'backend:sticker-animated'
        include 'backend:sticker-smart'
        include 'backend:background-removal'
    }
}

android {
    namespace "pantech.panshop_app"
    compileSdkVersion 35
    ndkVersion = "26.1.10909125"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8

        // Flag to enable support for the new language APIs
        // TODO: Used in the furture if lcoal noti get eorror
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "pantech.panshop_app"
        minSdkVersion 24 // HERE MAP SDK REQUIRED
        targetSdkVersion 35
        compileSdkVersion 35
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        multiDexEnabled true
    }

    signingConfigs {
        production {
            storeFile file('evm_keystore.jks')
            keyAlias 'EVM'
            keyPassword 'evm2023'
            storePassword 'evm2023'
        }
        staging {
            storeFile file('evm_staging_keystore.jks')
            keyAlias 'EVM Staging'
            keyPassword 'evmstg'
            storePassword 'evmstg'
        }
        dev {
            storeFile file('evm_dev_keystore.jks')
            keyAlias 'EVM Dev'
            keyPassword 'evmdev'
            storePassword 'evmdev'
        }
    }

    flavorDimensions "evmapp"

    productFlavors {
       dev {
            dimension "evmapp"// TODO: change
            applicationIdSuffix ".dev"
            resValue "string", "app_name", "PanshopDev"
            signingConfig signingConfigs.dev
        }
        staging {
            dimension "evmapp"// TODO: change
            applicationIdSuffix ".staging"
            resValue "string", "app_name", "PanshopStaging"
            signingConfig signingConfigs.staging
        }
        production {
            dimension "evmapp"// TODO: change
            applicationIdSuffix ".production"
            resValue "string", "app_name", "Panshop"
            signingConfig signingConfigs.production
        }
    }

    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.window:window:1.0.0'
    implementation 'androidx.window:window-java:1.0.0'

    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3' // support for local notification
}
